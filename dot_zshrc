### Sheldon plugin manager ###
eval "$(sheldon source)"
bindkey -e

function backward-kill-word-no-slash() {
  local WORDCHARS=${WORDCHARS//\/}
  zle backward-kill-word
}
zle -N backward-kill-word-no-slash
bindkey '^W' backward-kill-word-no-slash

# Cached compinit - only regenerate once per day
autoload -Uz compinit
_comp_path="${XDG_CACHE_HOME:-$HOME/.cache}/zsh/zcompdump-${ZSH_VERSION}"
[[ -d "${_comp_path:h}" ]] || mkdir -p "${_comp_path:h}"
if [[ -f "$_comp_path" && $(date +'%j') == $(stat -f '%Sm' -t '%j' "$_comp_path" 2>/dev/null) ]]; then
  compinit -C -d "$_comp_path"
else
  compinit -d "$_comp_path"
  touch "$_comp_path"
fi
unset _comp_path

### locale ###
export LANG="en_US.UTF-8"
export LC_COLLATE="en_US.UTF-8"
export LC_CTYPE="en_US.UTF-8"
export LC_MESSAGES="en_US.UTF-8"
export LC_MONETARY="en_US.UTF-8"
export LC_NUMERIC="en_US.UTF-8"

export EDITOR=nvim

### history ###
HISTFILE=$HOME/.zsh_history
HISTSIZE=100000
SAVEHIST=100000

### options ###
setopt share_history
setopt hist_ignore_dups
setopt hist_ignore_space
setopt hist_save_no_dups
setopt inc_append_history

setopt auto_cd
setopt extended_glob

### homebrew ###
#eval "$(/opt/homebrew/bin/brew shellenv)"

### Golang ###
case "$(uname -s)" in
  Linux)  export PATH="$PATH:/usr/local/go/bin" ;;
  Darwin)
    export GOPATH="$HOME/go"
    export GOBIN="$GOPATH/bin"
    export PATH="$PATH:$GOBIN"
    ;;
esac

### fzf history search ###
function fzf-select-history() {
	BUFFER=$(history -n 1 | \
		tail -r | \
		awk '!seen[$0]++' | \
		fzf --height=40% \
			--reverse \
			--border \
			--no-preview \
			--scheme=history \
			--query="$LBUFFER" \
			--prompt="History > ")
	CURSOR=$#BUFFER
	zle reset-prompt
}
zle -N fzf-select-history
bindkey '^r' fzf-select-history

# export commands
export PATH="$HOME/.local/bin:$HOME/commands:$(npm config get prefix)/bin:$PATH"

### Starship ###
if command -v starship >/dev/null 2>&1; then
  eval "$(starship init zsh)"
fi

##### alias #####
alias ll='lsd -l'
alias la='lsd -al'
alias cp='cp -i'
alias mv='mv -i'
alias rm='rm -i --preserve-root'
alias gp="git pull"
alias gf='git flow'
alias vim='nvim'
alias nvim='nvim'
alias vi='vi'
alias lg='lazygit'
alias cl='clear'
alias xh='xh -s monokai'
alias gitmo='gitmoji -c'
alias proot='cd $(git rev-parse --show-toplevel)'
alias sb='sunbeam'
alias ch='chezmoi'
alias nv='nohup neovide > /dev/null 2>&1 &'
alias cz='chezmoi'
alias awsts='aws sts get-caller-identity --no-cli-pager'

alias memol='memo list | fzf --preview "head ~/memo/{}" | xargs -I {} sh -c "open ~/memo/{} -a CotEditor"'
alias fzc="git branch --list | cut -c 3- | fzf --preview \"git log --pretty=format:'%h %cd %s' --date=format:'%Y-%m-%d %H:%M' {}\" | xargs git checkout"

function tmuxx {
	local session_name="${1:-$(date +%Y%m%d_%H%M)}"
	tmux new -s "$session_name"
}

if [ -f "$HOME/.env" ]; then
	source "$HOME/.env"

	if [ "$LOCAL_NAME" = "macbook" ]; then
		bfile="$HOME/ghq/github.com/maro114510/dotfiles/mac_book/Brewfile"
		alias brewd="brew bundle dump --force --file=$bfile"
	elif [ "$LOCAL_NAME" = "macmini" ]; then
		bfile="$HOME/ghq/github.com/maro114510/dotfiles/mac_mini/Brewfile"
		alias brewd="brew bundle dump --force --file=$bfile"
	elif [ "$LOCAL_NAME" = "job" ]; then
		bfile="$HOME/ghq/github.com/maro114510/dotfiles/job_macbook/Brewfile"
		alias brewd="brew bundle dump --force --file=$bfile"
	fi
fi

##### fzf #####
export FZF_DEFAULT_COMMAND='rg --files --hidden --follow --glob "!.git/*"'
export FZF_DEFAULT_OPTS="--height 50% --layout=reverse --border --inline-info"

# using ripgrep combined with preview
# vim with fzf
vf() {
	local file
	file=$(fzf --preview 'bat --style=numbers --color=always {}' --query="$1") &&
	nvim "${file}" || return 1
}
# fh - repeat history
fh() {
	eval $( ([ -n "$ZSH_NAME" ] && fc -l 1 || history) | fzf +s --tac | sed 's/ *[0-9]* *//' | sed 's/\\/\\\\/g' | awk '{print "echo exec: " $0 "; " $0}')
}
fgh() {
	local selected
	selected=$(ghq list | fzf --preview 'bat --color=always --style=header,grid --line-range :500 $(ghq root)/{}/README.*')

	if [ "x$selected" != "x" ]; then
		cd $(ghq root)/$selected
	fi
}
fkill() {
	local pid
	pid=$(ps -ef | sed 1d | fzf -m | awk '{print $2}')

	if [ "x$pid" != "x" ]
	then
		echo $pid | xargs kill -${1:-9}
	fi
}
# Delete multiple files with FZF
frm() {
    ls -al | fzf -m | xargs -I {} rm {}
}
# Search with Browser
# https://s10i.me/whitenote/post/40
# gog() {
#     if [ $(echo $1 | grep "^-[cfs]$") ]; then
#         local opt=$1
#         shift
#     fi
#     local url="https://google.co.jp/search?q=${*// /+}"
#     local c="Google Chrome"
#     local f="Firefox"
#     local s="Safari"
#     case $opt in
#         -c ) open $url -a $c;;
#         -f ) open $url -a $f;;
#         -s ) open $url -a $s;;
#         * ) open $url;;
#     esac
# }
# weather
function wtr(){
    curl "https://ja.wttr.in/$1?2nF"
}

# mise for language version management (shims mode for faster startup)
eval "$(mise activate zsh --shims)"

### zoxide
if command -v zoxide >/dev/null 2>&1; then
  eval "$(zoxide init zsh)"
  z()  { builtin cd -- "$(zoxide query -i -- "$@")" }
fi

if command -v direnv >/dev/null 2>&1; then
  eval "$(direnv hook zsh)"
fi

### bmm
function bmb() {
	# ブラウザでfzfで選択したブックマーク(URL)を開く
	local bookmark_url
	bookmark_url=$(bmm list | fzf --preview 'bmm show {}')
	open -a "Google Chrome" "$bookmark_url"
}

### aws profile
alias awsp=set_aws_profile

function set_aws_profile() {
	# Select AWS PROFILE
	local selected_profile=$(aws configure list-profiles |
		grep -v "default" |
		sort |
		fzf --prompt "Select PROFILE. If press Ctrl-C, unset PROFILE. > " \
		--height 50% --layout=reverse --border --preview-window 'right:50%' \
		--preview "grep {} -A5 ~/.aws/config")

	export AWS_PROFILE=$selected_profile
}

function y() {
	local tmp cwd
	tmp="$(mktemp -t "yazi-cwd.XXXXXX")"

	command yazi "$@" --cwd-file="$tmp"

	if IFS= read -r -d '' cwd < "$tmp" && [ -n "$cwd" ] && [ "$cwd" != "$PWD" ]; then
		builtin cd -- "$cwd"
	fi

	if [ -n "$tmp" ] && [ -e "$tmp" ]; then
		command trash "$tmp"
	fi
}

function paws {
    local profile=$(aws configure list-profiles | \
        grep -v "default" | \
        sort | \
        fzf --prompt "Select AWS PROFILE > " \
            --height 50% --layout=reverse --border \
            --preview "grep {} -A5 ~/.aws/config")
    if [ -n "$profile" ]; then
        AWS_PROFILE=$profile
        export AWS_PROFILE
        echo "AWS_PROFILE is now $AWS_PROFILE"
    fi
}

function penv {
    local appenv=$(echo -e "development\nstaging\nproduction" | \
        fzf --prompt "Select APP_ENV > " \
            --height 30% --layout=reverse --border)
    if [ -n "$appenv" ]; then
        APP_ENV=$appenv
        export APP_ENV
        echo "APP_ENV is now $APP_ENV"
    fi
}
